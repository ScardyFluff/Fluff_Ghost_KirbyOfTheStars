
//--- kirby_bootend.dic -------------------------------------------------------------

OnFirstBoot //\![raise,OnFirstBoot,0]
{
 	lastTalk = ""
	passhour = 0
	passmin = 0 
	passsec = 0
 	username = "friend"
	stroke = 0
 	teachusername = 0
	mikireflag = 0
 	aitalkinterval = 300
 	birthdayprint = "?????"
	talktime = "5 minutes"
	deleteshitai = "OFF"
	nowshell = "master"
	copyabilityON = 0
	archerON = 0
	artistON = 0
	firstboot = 1

	if reference0 == 0
 	{
		// These lines go to kirby_nameteach.dic and does everything there.
		"\![enter,passivemode]"
		--
		"\![bind,Ability,,0]\0\s[0]\1\s[10]"
		--
		"Name?\w5\![open,inputbox,OnNameTeach,-1]\e"
 	}
	else
 	{
 		{
			//This line is for when Kirby was Uninstalled and then Reinstalled.
			"\0\s[0]I'm back.\e"
		}
 	}
}


timeslot
{
	if hour >= 5 && hour <= 8
	{	"earlymorning"	}
	elseif hour >= 9 && hour <= 11
	{	"morning"	}
	elseif hour >= 12 && hour <= 14
	{	"lunch"	}
	elseif hour >= 15 && hour <= 17
	{	"afternoon"	}
	elseif hour >= 18 && hour <= 20
	{	"evening"	}
	elseif hour >= 21 && hour <= 24
	{ "latenight"	}
	else
	{ "midnight" }
}


dayslot
{
	"%(month)month %(day)day"
}


//--- Normal Boot -------------------------------------------------------------------

OnBoot
{
//Variables to reset on a normal boot.
	lastTalk = ""
	
	copyabilityON = 0
	archerON = 0
	artistON = 0
	
	passhour = 0
	passmin = 0
	passsec = 0

//this sets it up so Kirby can appear with different copy abilities on boot.
	if RAND(100) >= 50
	{
		copyabilityON = 1
		_copyAbilityRandomizer = RAND(3)

		case _copyAbilityRandomizer
		{
			when 0
			{
				archerON = 1
				"\0\s[0]\![bind,Ability,Archer,1]Archer\1\s[10]"
			}
			when 1
			{
				artistON = 1
				"\0\s[0]\![bind,Ability,Artist,1]Artist\1\s[10]"
			}
			others
			{
				copyabilityON = 0
				"\0\s[0]\![bind,Ability,,0]The first randomizer rolled more than 50, but the second one rolled 3.\1\s[10]"
			}
		}
	}
	else
	{
		"\0\s[0]\![bind,Ability,,0]It rolled less than 50.\1\s[10]"
	}
	--
	//this will check if it's the user's birthday!
	if dayslot == userbirthday
	{
		"\0\s[0]It's %(bornmonthprint) %(borndayprint).\w8 Happy birthday, %(username).\e" 
	}
	elseif dayslot == "6month 6day"
	{
		"\0\s[0]Today is June 6th.\e"
	}
	elseif dayslot == "7month 4day"
	{
		"\1\s[10]It's the fourth of July.\e"
	}
	else
	{
		//if none of the above are true, go to here and continue to "normalboottalk"
		"%(normalboottalk)"
	}
}


normalboottalk
{
	//This checks if the weekday equals six, meaning saturday AND (using the && operator) if the hour is past 19. You can change these numbers or values to anything you like! As such you can check any combination of date and day and time for potential boot dialogue. You can also add more elseif checks for other days if you want.
	if weekday == 6 && hour >= 19
		{
			"\0\s[0]It's Saturday night.\e"
		}
	//elseif RAND(100) < 40
		//{
		//	"\0\s[0]This is one of the random boot conversations.\w8\1\s[10]The random number drawn was less than forty.\e"
		//}
 	else
 	{
		//The following if statements check the time of the day as defined above in the GetTimeSlot function. Do you remember the names that it defined? Now we're going to use them!
		//The following dialogue will be called if they are booted during the time specified in GetTimeSlot (in this case, the hour being more than/equal to 5 but less than/equal to 8). Remember, you can add as many lines of dialogue here as you want, or take away as many as you want! Replace the dialogue here with what you see fit.
		if timeslot == "earlymorning" 
		{
			"\0\s[0]This is an early morning boot dialogue.\e"
		}
		elseif timeslot == "morning"
		{
			"\0\s[0]This is a morning boot dialogue.\e"
		}
		elseif timeslot == "lunch"
		{
			"\0\s[0]This is a lunch boot dialogue.\e"
		}
		elseif timeslot == "afternoon"
		{
			"\1\s[10]This is an afternoon boot dialogue.\e"
		}
		elseif timeslot == "evening"
		{
			"\0\s[0]This is an evening boot dialogue.\e"
		}
		elseif timeslot == "latenight"
		{
			"\0\s[0]This is a late night boot dialogue.\e"
		}
		else
		{
			"\0\s[0]This is a middle of the night boot dialogue.\e"
		}
	}
}


OnClose
 {
	//This checks to see if you've had them open for at least five minutes.
	if passmin >= 5 || passhour > 0
		{
			if RAND(100) < 40
			{
				"\0\s[0]The random number generator chose something below 40.\w8\w8"
			}
			else
			{
				if timeslot == "earlymorning"
				{
					"\1\s[10]This is an early morning close dialogue.\w8\0\s[0]From 5 to 8.\w8"
					
					"\0\s[0]This is another early morning close dialogue.\w8\1\s[10]Yup.\w8\w8"
				}
				elseif timeslot == "morning"
 				{
 					"\0\s[0]This is a morning close dialogue.\w8\w8"
 				}
				elseif timeslot == "lunch"
 				{
 					"\0\s[0]This is a lunch close dialogue.\w8\1\s[10]Yup.\w8"
					
 					"\0\s[0]This is another lunch close dialogue.\w8\1\s[10]In the middle of the day.\w8"
 				}
				elseif timeslot == "afternoon"
 				{
 					"\0\s[0]This is an afternoon close dialogue.\w8\1\s[10]Done.\w8"
					
 					"\1\s[10]This is another afternoon close dialogue.\w8\0\s[0]Yup.\w8"
 				}
				elseif timeslot == "evening"
 				{
 					if weekday == 6 //here, a nested if statement is checking that if it's evening, is it also saturday? You can do this to check more specific times on other days as well. You can change this to some other day if you want, or just delete this little if/else section entirely and write your own evening related close dialogue.
 					{
						"\0\s[0]This is a Saturday evening close dialogue.\w8\1\s[10]Somewhat specific.\w8"
 					}
 					else //if it's not saturday, then it'll do this dialogue instead.
 					{
 						"\0\s[0]This is an evening close dialogue.\w8\1\s[10]Bye.\w8"
	
 						"\1\s[10]This is another evening close dialogue.\w8\0\s[0]Yup.\w8"
 					}
 				}
				elseif timeslot == "latenight"
			 	{
 					if weekday == 6 //This is the same check as above to see if it's Saturday. Note that this doesn't have an else statement! Sometimes you can get away with that, but make sure you test thoroughly.
					//Of course, you can also cut this check entirely if you want.
 					{
						"\0\s[0]This is a late Saturday night close dialogue.\w8"
 					}
 					"\1\s[10]This is a late night close dialogue.\w8\0\s[0]That it is.\w8"
 				}
				else //like above, this is the midnight time slot since it's the last one left.
 				{
 					if weekday == 0 //checking if it's the middle of the night on Sunday
 					{
 						"\1\s[10]This is a middle of the night Sunday close dialogue.\w8\0\s[0]From midnight to five AM on Sunday.\w8"
 					}
					"\0\s[0]This is a middle of the night close dialogue.\w8\1\s[10]Very late at night.\w8"

					"\1\s[10]This is another middle of the night close dialogue.\w8"

					"\1\s[10]This is the third middle of the night close dialogue.\w8\0\s[0]So it is.\w8"
				}
			}	
 		}
 	else
 	{
		//This dialogue is for if they haven't been open for at least five minutes.
 		"\0\s[0]Not even five minutes have passed...\w8\w8"
 	}
 	--
	//this bit of code actually closes and ends the ghost. Because \e is here, you don't have to end each line above with it. Again, this will come up very rarely, so OnClose is an unusual case.
	"\-\e"
 }


//--- State Changes -----------------------------------------------------------------

//This will run when you minimize them. Replace with what you like.
 OnWindowStateMinimize
 {
	"\0\s[0]Minimized!\e"
}


//This will run when you minimize them and then bring them back up. Replace with what you like.
OnWindowStateRestore
 {
	"\0\s[0]Maximized!\e"
}


//This runs whenever you change or reload the ghost. You can change ghosts by going into the right click menu and going to Change Ghost, but this dialogue will also be called if you reload them using Utilities->Reload Ghost or the Developer's Console->Reload->Ghost. Protip: If you are going to make a ghost, you will be reloading it A LOT.
//Note: If you reload your ghost using the developer console instead of Utilities->Reload Ghost, it won't do this dialogue, so don't freak out if it doesn't show up.
OnGhostChanging
 {
	if reference0 == "Kirby"
	{
		"\0\s[0]I will reload.\w8\w8\e"
	}
 	else
	{
		"\0\s[0]I'm swapping to %(reference0).\w8\w8\e"
	}
}


//A sister to the above, this runs after the ghost has either reloaded itself, or when you switch from another ghost to this ghost.
OnGhostChanged
{
	"\0\s[0]\1\s[10]"
	--
	if reference0 == "Kirby"
	{
		"\0\s[0]I have reloaded.\1\s[10]\e"
	}
	else
	{
		"\0\s[0]Bye, %(reference0).\1\s[10]\e"
	}
}


// Whenever you change Kirby's shells, he will say these!
OnShellChanging
 {
	if reference0 == "classic"
	{
	"\o\s[0]Changing to Classic.\w8\e"
	}
	elseif reference0 == "cherry"
	{
	"\o\s[0]Changing to Cherry Paint.\w8\e"
	}
	else
	{
	"\0\s[0]Ooh! how am I gonna look like now? \w5I can't wait!\w8\e"
	}
 }
 

// After you change Kirby's shell, and he returns, he will say these!
// Note: reference0 is the name of the shell, while nowshell is the folder's name!
OnShellChanged
 {
 	if reference0 == "Modern"
 	{
 		nowshell = "master"

 		"\0\s[0]Back to my usual look.\e"
	}
 	elseif reference0 == "Classic"
 	{
 		nowshell = "classic"
		
 		"\0\s[0]Original look.\e"
 	}
 	elseif reference0 == "Cherry Paint"
 	{
 		nowshell = "cherry"
		
 		"\0\s[0]The Cherry look.\e"
 	}
 	else
 	{
	"\0\s[0]I swapped to %(reference[0]).\e"
	//Note the %(reference[0]) here! This time that should store the name of whatever shell you're switching to. Replace this bit of dialogue as you need.
	}
 }


 //--OnDressupChanged
 
 //This runs whenever you fiddle with their clothing via the dress-up option, if you've set any for your ghost. Make sure to read the clothing page on the walkthrough for details on how clothes work! This template won't have any because Girl and Triangle move around too much for it, but if your ghost is mostly stationary, clothes are an option. In that case, these functions are for you. For now, I'll fill this in with some hypothetical items. Replace the dialogue below with whatever you need.

OnDressupChanged
{
if reference0 == 0 //in OnDressupChanged, reference0 refers to the character, therefore this is evaluating which character in question is getting dressed up. 0 is the main character, as you know from the walkthrough by now.
	{
	if reference1 == "Clothing for Girl" //This is the name of the clothing item in the descript.txt file, I'm pretty sure.
		{
			if reference2 == 1 //This checks if you're putting it on.
			{
			"\0\s[0]You put my clothing item on me.\w8\1\s[19]lol like zar would ever bother with clothes for a ghost.\w8\0\s[9]\n\n[half]Shut up dude you're going to get us in trouble.\e"
			}
			else // This checks if you're taking it off.
			{
			"\0\s[0]You took the clothing item off.\e"
			}
		}		
	}
else //if reference0 isn't the main character, then it has to be the side character.
	{
	if reference1 == "some kind of clothing for triangle??" //The name of the item of clothing in the descript.txt file in your shell folder.
		{
			if reference2 == 1 //If you put it on
			{
			"\1\s[10]I'm wearing some kind of clothing.\w8\0\s[8]Maybe a hat?\e"
			}
			else //When you take it off.
			{
			"\1\s[10]You took off some kind of clothing.\e"
			}
		}
	elseif reference1 == "i'm gonna say a propeller beanie" //As you've seen above, you can use elseifs to expand the list if you have more than one item.
		{
			if reference2 == 1 //if you're putting it on
			{
			"\1\s[10]You put a hat on me.\w8\0\s[6]lol.\w8\1\n\n[half]\s[11]Shaddap.\e"
			}
			else //when you take it off
			{
			"\1\s[10]You took off the hat.\e"
			}
		}
	}
}